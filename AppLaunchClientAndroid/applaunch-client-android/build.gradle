apply plugin: 'com.android.library'
apply plugin: 'signing'
apply plugin: 'maven'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 7
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent, "applaunch-client-android.aar")
        }
    }
}



task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs

    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/internal/**'

    options.links 'http://docs.oracle.com/javase/7/docs/api/'
    options.encoding 'UTF-8'
    options.memberLevel = JavadocMemberLevel.PUBLIC

    setTitle 'IBM Cloud Mobile Services App Launch Android API'

    failOnError false
}

task androidJavadocsJar(type: Jar, dependsOn: generateJavadoc) {
    classifier ='javadoc'
    from generateJavadoc.destinationDir
}

task androidSourcesJar(type: Jar){
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

signing {
    required { isReleaseBuild() }
    sign configurations.archives
}

def getRepositoryUsername() {
    return hasProperty('ossrhUsername') ? ossrhUsername :''
}

def getRepositoryPassword() {
    return hasProperty('ossrhPassword') ? ossrhPassword : ''
}

def getReleaseRepositoryUrl() {
    return 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
}

def getSnapshotRepositoryUrl() {
    return 'https://oss.sonatype.org/content/repositories/snapshots/'
}

def isReleaseBuild() {
    return !getVersion().contains('-SNAPSHOT')
}

def getVersion() {
    return '1.0.0-SNAPSHOT'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: getReleaseRepositoryUrl()) {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            snapshotRepository(url: getSnapshotRepositoryUrl()) {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            pom.groupId='com.ibm.mobile.applaunch.android'
            pom.artifactId='applaunch'
            pom.version = getVersion()

            pom.project {
                name 'IBM Cloud Mobile Services - Client SDK Android App Launch'
                description 'This is the App Launch component of the Android SDK for IBM Cloud Mobile Services.'
                url 'https://github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-applaunch'

                scm {
                    connection 'scm:git:git@github.com:ibm-bluemix-mobile-services/bms-clientsdk-android-applaunch.git'
                    developerConnection 'scm:git:git@github.com:ibm-bluemix-mobile-services/bms-clientsdk-android-applaunch.git'
                    url 'https://github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-applaunch'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'IBM Cloud Services Mobile SDK'
                        email 'mobilsdk@us.ibm.com'
                    }
                }
            }
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.ibm.mobilefirstplatform.clientsdk.android:core:[2.0.0,3.0.0)'
    compile 'com.squareup.picasso:picasso:+'
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
}
